<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>FTBXPRMSelfServiceRoleRequestTypeSubmitManager</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The FTBXPRMSelfServiceRoleRequestTypeSubmitManager menu item action event handler.
/// </summary>
internal class FTBXPRMSelfServiceRoleRequestTypeSubmitManager 
{
    protected FTBXPRMSelfServiceRoleRequests    roleRequest;
    protected WorkflowVersionTable              workflowConfigurationTable;
    protected WorkflowComment                   workflowComment;
    protected boolean                           menuItemSubmit;
    protected WorkflowWorkItemTable             workflowWorkItemTable;
    protected MenuItemName                      menuItemName;
    protected WorkflowTypeName                  workflowTemplateName;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>dialogOk</Name>
				<Source><![CDATA[
    /// <summary>
    /// Runs the workflow dialog
    /// </summary>
    /// <returns>The submitter accepted the workflow submission</returns>
    public boolean dialogOk()
    {
        WorkflowSubmitDialog workflowSubmitDialog = WorkflowSubmitDialog::construct(workflowConfigurationTable);
        workflowSubmitDialog.run();
        this.workflowComment = workflowComment;
        return workflowSubmitDialog.parmIsClosedOK();
    }

]]></Source>
			</Method>
			<Method>
				<Name>init</Name>
				<Source><![CDATA[
    /// <summary>
    /// Initialize the workflow manager
    /// </summary>
    /// <param name = "_documentRecord">The PRM role request record</param>
    /// <param name = "_menuItemName">Menu item the submission was called from</param>
    /// <param name = "_workflowConfigurationTable">Workflow configuration</param>
    /// <param name = "_workflowWorkItemTable">Configured menu item of the workflow type</param>
    public void init(FTBXPRMSelfServiceRoleRequests _documentRecord,
                     menuItemName                   _menuItemName,
                     WorkflowVersionTable           _workflowConfigurationTable,
                     WorkflowWorkItemTable          _workflowWorkItemTable)
    {
        this.roleRequest    = _documentRecord;
        this.menuItemSubmit = _menuItemName == menuitemactionstr(FTBXPRMSelfServiceRoleRequestTypeSubmitMenuItem);
        this.menuItemName   = _menuItemName;
        
        this.workflowConfigurationTable = _workflowConfigurationTable;
        this.workflowWorkItemTable      = _workflowWorkItemTable;
        this.workflowTemplateName       = _workflowConfigurationTable.workflowTable().TemplateName;
    }

]]></Source>
			</Method>
			<Method>
				<Name>submit</Name>
				<Source><![CDATA[
    /// <summary>
    /// Submits the role request to workflow
    /// </summary>
    public void submit()
    {
        Workflow::activateFromWorkflowType(this.workflowTemplateName,
            roleRequest.RecId,
            workflowComment,
            NoYes::No);

        FTBXPRMSelfServiceRoleRequest::construct(roleRequest.RequestId)
            .setStatus(FTBXPRMSelfServiceRoleRequestStatus::Submitted)
            .commit();
    }

]]></Source>
			</Method>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    protected void new()
    {
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    /// <summary>
    /// Constructs the workflow manager
    /// </summary>
    /// <returns>An instantiated copy of the <c>FTBXPRMSelfServiceRoleRequestTypeSubmitManager</c> class</returns>
    public static FTBXPRMSelfServiceRoleRequestTypeSubmitManager construct()
    {
        return new FTBXPRMSelfServiceRoleRequestTypeSubmitManager();
    }

]]></Source>
			</Method>
			<Method>
				<Name>validateArgs</Name>
				<Source><![CDATA[
    /// <summary>
    /// Validates the args the manager was called with
    /// </summary>
    /// <param name = "_args">Args of the menu item</param>
    /// <returns>Returns true if all is okay</returns>
    public boolean validateArgs(Args _args)
    {
        boolean ret =
            _args.caller()
            && _args.caller() is FormRun
            && _args.record()
            && _args.record() is FTBXPRMSelfServiceRoleRequests;

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>main</Name>
				<Source><![CDATA[
    /// <summary>
    /// Entry point of the workflow manager
    /// </summary>
    /// <param name = "_args">Arguments from the caller</param>
    public static void main(Args _args)
	{
        var manager = FTBXPRMSelfServiceRoleRequestTypeSubmitManager::construct();

        if (manager.validateArgs(_args))
        {
            var formRun = _args.caller() as FormRun;

            manager.init(
                _args.record(),
                _args.menuItemName(),
                formRun.getActiveworkflowConfiguration(),
                formRun.getActiveWorkflowWorkItem());

            if (manager.roleRequest.canSubmitToWorkflow()
                && manager.dialogOk()
                && manager.menuItemSubmit)
            {
                manager.submit();

                formRun.updateWorkflowControls();
                formRun.doRefresh();
            }
        }
	}

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>