<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>FTBXPRMSelfServiceRoleRequest</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// Representation of a PRM role request
/// This class contains functionality related to 
///     Creation of role request
///     Submision of request to workflow
///     Marking of request as expired
/// </summary>
public final class FTBXPRMSelfServiceRoleRequest extends FTBXPRMInterfaceBase
{
    internal FTBXPRMSelfServiceRoleRequests     roleRequest;
    internal FTBXPRMSelfServiceRoleRequestNotes roleRequestsNote;
    internal FTBXPRMTmpReferences               tmpRoleReferences;  


}
]]></Declaration>
		<Methods>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    protected void new(FTBXPRMSelfServiceRoleRequestId _requestId = '')
    {
        if (_requestId)
        {
            select firstonly roleRequest
                where roleRequest.RequestId == _requestId
                join roleRequestsNote
                    where roleRequestsNote.RequestId == roleRequest.RequestId;

            FTBXPRMSelfServiceRoleRequestReferences references;

            insert_recordset tmpRoleReferences (ReferenceType, Reference)
                select ReferenceType, Reference from references
                where references.RequestId == roleRequest.RequestId;
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    public static FTBXPRMSelfServiceRoleRequest construct(FTBXPRMSelfServiceRoleRequestId _requestId = '')
    {
        return new FTBXPRMSelfServiceRoleRequest(_requestId);
    }

]]></Source>
			</Method>
			<Method>
				<Name>setUserId</Name>
				<Source><![CDATA[
    /// <summary>
    /// Sets the user of the role request
    /// </summary>
    /// <param name = "_userId">User that the request has been created for</param>
    /// <returns>This</returns>
    public FTBXPRMSelfServiceRoleRequest setUserId(UserId _userId)
    {
        roleRequest.UserId = _userId;
        return this;
    }

]]></Source>
			</Method>
			<Method>
				<Name>setRole</Name>
				<Source><![CDATA[
    /// <summary>
    /// Role that the user has requested, or on behalf of the user
    /// </summary>
    /// <param name = "_roleId">RecId of the role</param>
    /// <returns>This</returns>
    public FTBXPRMSelfServiceRoleRequest setRole(SysSecurityRoleRecId _roleId)
    {
        roleRequest.SysSecurityRoleRecId = _roleId;
        return this;
    }

]]></Source>
			</Method>
			<Method>
				<Name>setFromDateTime</Name>
				<Source><![CDATA[
    /// <summary>
    /// From date/time of the request
    /// </summary>
    /// <param name = "_validFrom">From date/time of the request</param>
    /// <returns>This</returns>
    public FTBXPRMSelfServiceRoleRequest setFromDateTime(FromDateTime _validFrom)
    {
        isDirty = true;

        roleRequest.FromDateTime = _validFrom;
        return this;
    }

]]></Source>
			</Method>
			<Method>
				<Name>setToDateTime</Name>
				<Source><![CDATA[
    /// <summary>
    /// To date/time of the request
    /// </summary>
    /// <param name = "_validTo">To date/time of the request</param>
    /// <returns>This</returns>
    public FTBXPRMSelfServiceRoleRequest setToDateTime(ToDateTime _validTo)
    {
        isDirty = true;

        roleRequest.ToDateTime = _validTo;
        return this;
    }

]]></Source>
			</Method>
			<Method>
				<Name>setEligibilityId</Name>
				<Source><![CDATA[
    /// <summary>
    /// Sets the reference to the role assignment eligibility record
    /// </summary>
    /// <param name = "_eligibilityId">Role eligibility identifier</param>
    /// <returns>This</returns>
    public FTBXPRMSelfServiceRoleRequest setEligibilityId(FTBXPRMSelfServiceRoleEligibilityId _eligibilityId)
    {
        roleRequest.EligibilityId = _eligibilityId;
        return this;
    }

]]></Source>
			</Method>
			<Method>
				<Name>setJustification</Name>
				<Source><![CDATA[
    /// <summary>
    /// Sets the justification of why the user should have this role
    /// </summary>
    /// <param name = "_justification">Request justification</param>
    /// <returns>This</returns>
    public FTBXPRMSelfServiceRoleRequest setJustification(Notes _justification)
    {
        roleRequestsNote.justification = _justification;
        return this;
    }

]]></Source>
			</Method>
			<Method>
				<Name>setDuration</Name>
				<Source><![CDATA[
    /// <summary>
    /// Sets the duration of the role request in hours
    /// </summary>
    /// <param name = "_duration">Request duration in hours</param>
    /// <returns>This</returns>
    public FTBXPRMSelfServiceRoleRequest setDuration(FTBXPRMSelfServiceDuration _duration)
    {
        roleRequest.Duration = _duration;
        return this;
    }

]]></Source>
			</Method>
			<Method>
				<Name>addReference</Name>
				<Source><![CDATA[
    /// <summary>
    /// Adds a reference to the role request
    /// </summary>
    /// <param name = "_refType">Reference type (E.g. ticket, application)</param>
    /// <param name = "_reference">Reference value</param>
    /// <returns>This</returns>
    public FTBXPRMSelfServiceRoleRequest addReference(
        FTBXPRMSelfServiceRoleRequestReferenceType  _refType,
        FTBXRPMSelfServiceRoleRequestReference      _reference)
    {
        tmpRoleReferences.ReferenceType = _refType;
        tmpRoleReferences.Reference     = _reference;
        tmpRoleReferences.insert();

        return this;
    }

]]></Source>
			</Method>
			<Method>
				<Name>reload</Name>
				<Source><![CDATA[
    /// <summary>
    /// Reloads the request. Typically used if a request state has changed
    /// </summary>
    public void reload()
    {
        super();

        roleRequest.reread();
        roleRequestsNote.reread();
    }

]]></Source>
			</Method>
			<Method>
				<Name>setStatus</Name>
				<Source><![CDATA[
    /// <summary>
    /// Sets the state of the request
    /// The request state changes contains the business logic of the request lifecycle
    /// </summary>
    /// <param name = "_status">Status to change to</param>
    /// <returns>This</returns>
    public FTBXPRMSelfServiceRoleRequest setStatus(FTBXPRMSelfServiceRoleRequestStatus _status)
    {
        if (roleRequest.RequestStatus != _status)
        {
            roleRequest.RequestStatus = _status;

            this.isDirty = true;
            this.statusChanged = true;
        }

        return this;
    }

]]></Source>
			</Method>
			<Method>
				<Name>setNumSeq</Name>
				<Source><![CDATA[
    /// <summary>
    /// Sets the identifier of the role request
    /// Will throw an exception if no number sequence has been configured
    /// </summary>
    private void setNumSeq()
    {
        NumberSequenceReference numSeqRefRequest = FTBXPrivilegedRoleManagementParameters::numRefFTBXPRMSelfServiceRoleRequestId();
        
        if (numSeqRefRequest && numSeqRefRequest.NumberSequenceId)
        {
            numberSeq = NumberSeq::newGetNum(numSeqRefRequest, true);
            roleRequest.RequestId = numberSeq.num();
        }
        else
        {
            throw error("@SYS53912");
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>submitToWorkflow</Name>
				<Source><![CDATA[
    /// <summary>
    /// Role workflow submission rogic
    /// If approval has not been enabled, an assignment record will be created immediately and approved.
    /// </summary>
    public void submitToWorkflow()
    {
        if (roleRequest.canSubmitToWorkflow()
            && Workflow::findWorkflowConfigToActivateForType(workflowTypeStr(FTBXPRMSelfServiceRoleRequestType),roleRequest.RecId, roleRequest.TableId))
        {
            this.setStatus(FTBXPRMSelfServiceRoleRequestStatus::Submitted)
                .commit();
        }
        else if (!FTBXPrivilegedRoleManagementParameters::find().RoleRequestRequireApprovals)
        {
            this.setStatus(FTBXPRMSelfServiceRoleRequestStatus::Approved)
                .commit();

            this.setStatus(FTBXPRMSelfServiceRoleRequestStatus::Assigned)
                .commit();            
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>expireAssignments</Name>
				<Source><![CDATA[
    /// <summary>
    /// Expires the related role assignment
    /// </summary>
    public void expireAssignments()
    {
        FTBXPRMRoleAssignments roleAssignments;

        while select roleAssignments
            where roleAssignments.RequestId == roleRequest.RequestId
        {
            FTBXPRMRoleAssignment::construct(roleAssignments.AssignmentId)
                .removeRoleFromUser()
                .setToDateTime(DateTimeUtil::addSeconds(DateTimeUtil::getSystemDateTime(), -1))
                .commit();
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>commitInternal</Name>
				<Source><![CDATA[
    /// <summary>
    /// Commits changes to database, and trigger business logic related to status changes
    /// </summary>
    protected void commitInternal()
    {
        if (!roleRequest.RecId)
        {
            this.setNumSeq();

            if (roleRequest.validateWrite())
            {
                roleRequest.insert();
                roleRequestsNote.RequestId  = roleRequest.RequestId;
                roleRequestsNote.insert();

                if (tmpRoleReferences)
                {
                    FTBXPRMSelfServiceRoleRequestReferences references;
                    FTBXPRMSelfServiceRoleRequestId         curRequestId = roleRequest.RequestId;

                    insert_recordset references (RequestId, Reference, ReferenceType)
                            select curRequestId, Reference, ReferenceType from tmpRoleReferences;
                }

                numberSeq.used();

                isDirty         = false;
                statusChanged   = false;

                this.submitToWorkflow();
            }
        }
        else if (isDirty)
        {
            isDirty = false;

            if (roleRequest.validateWrite())
            {
                roleRequest.selectForUpdate(true);
                roleRequest.update();
                roleRequest.selectForUpdate(false);

                if (statusChanged)
                {   
                    statusChanged = false;

                    FTBXPRMSelfServiceRoleRequestState::construct(this, roleRequest.RequestStatus).changeState();
                }
            }
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>