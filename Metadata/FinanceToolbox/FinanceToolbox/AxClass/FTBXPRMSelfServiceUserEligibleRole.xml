<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>FTBXPRMSelfServiceUserEligibleRole</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// Representation of an user role eligibility record
/// Pattern documentation: https://melgaard.nu/x-readability-custom-types/
/// </summary>
internal final class FTBXPRMSelfServiceUserEligibleRole
{
    public System.String                        userId;
    public RefRecId                             roleId;
    public str                                  roleEligibilityId;
    public FromDateTime                         fromDateTime;
    public ToDateTime                           toDateTime;
    public FTBXPRMSelfServiceMaxDuration        maxDuration;

    FTBXPrivilegedRoleManagementParameters parameters;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    protected void new(
        UserId                  _userId, 
        SysSecurityRoleRecId    _roleId)
    {
        userId  = _userId;
        roleId  = _roleId;

        parameters = FTBXPrivilegedRoleManagementParameters::find();
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    /// <summary>
    /// Constructor for FTBXPRMSelfServiceUserEligibleRole
    /// </summary>
    /// <param name = "_userId">User of the eligibility record</param>
    /// <param name = "_roleId">Role that the eligibility record contains</param>
    /// <returns>An instantiated copy of the <c>FTBXPRMSelfServiceUserEligibleRole</c> class</returns>
    public FTBXPRMSelfServiceUserEligibleRole construct(
        UserId                  _userId,
        SysSecurityRoleRecId    _roleId)
    {
        return new FTBXPRMSelfServiceUserEligibleRole(_userId, _roleId);
    }

]]></Source>
			</Method>
			<Method>
				<Name>setUserEligibilityRecord</Name>
				<Source><![CDATA[
    /// <summary>
    /// Initializes the class for an role eligibility record
    /// </summary>
    /// <param name = "_eligibleRole">User role eligibility record</param>
    /// <returns>This</returns>
    public FTBXPRMSelfServiceUserEligibleRole setUserEligibilityRecord(FTBXPRMSelfServiceEligibleRoles _eligibleRole)
    {
        roleEligibilityId   = _eligibleRole.RoleEligibilityId;
        fromDateTime        = max(DateTimeUtil::getSystemDateTime(), FTBXPRMSelfServiceUserEligibleRole::getEarliestStartDate(userId, roleId));

        maxDuration         = parameters.RoleRequestsMaxDuration    ? parameters.RoleRequestsMaxDuration                : _eligibleRole.MaxDuration;        
        toDateTime          = maxDuration && FromDateTime           ? DateTimeUtil::addMinutes(fromDateTime, real2int(maxDuration) * 60): DateTimeUtil::minValue();

        return this;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getEarliestStartDate</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the earliest possible start date of a role request
    /// </summary>
    /// <param name = "_userId">User that requests a role</param>
    /// <param name = "_roleId">The role that is requested</param>
    /// <returns>The earliest possible date/time</returns>
    public static utcdatetime getEarliestStartDate(UserId _userId, SysSecurityRoleRecId _roleId)
    {
        utcdatetime                     ret;
        FTBXPRMSelfServiceRoleRequests  roleRequests;
        FTBXPRMRoleAssignments          roleAssignments;        

        select firstonly ToDateTime from roleRequests
            order by roleRequests.ToDateTime desc
            where roleRequests.UserId               == _userId
            &&    roleRequests.SysSecurityRoleRecId == _roleId
            &&    roleRequests.RequestStatus        != FTBXPRMSelfServiceRoleRequestStatus::Expired
            &&    roleRequests.RequestStatus        != FTBXPRMSelfServiceRoleRequestStatus::Rejected
            &&    roleRequests.ToDateTime           != DateTimeUtil::maxValue();

        ret = roleRequests.ToDateTime;

        select firstonly ToDateTime from roleAssignments
            order by roleAssignments.ToDateTime desc
            where roleAssignments.UserId                == _userId
            &&    roleAssignments.SysSecurityRoleRecId  == _roleId
            &&    roleAssignments.RequestId             == ''
            &&    roleAssignments.ToDateTime            != DateTimeUtil::maxValue();

        if (roleAssignments.ToDateTime != DateTimeUtil::minValue()
            && roleAssignments.ToDateTime > ret)
        {
            ret = roleAssignments.ToDateTime;
        }

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>appendToSetIfNotExists</Name>
				<Source><![CDATA[
    /// <summary>
    /// Appends to a set of role eligibility records, based on if the overlapping parameters is set or not
    /// </summary>
    /// <param name = "_set">A set of <c>FTBXPRMSelfServiceUserEligibleRole</c> objects</param>
    /// <returns>This</returns>
    public FTBXPRMSelfServiceUserEligibleRole appendToSetIfNotExists(Set _set)
    {
        if (!(!parameters.RoleRequestsAllowOverlappingRoles
            && FTBXPRMSelfServiceRoleRequests::existsOpenUserRole(this.userId, this.roleId)))
        {
            if (!_set.in(this))
            {
                _set.add(this);
            }
        }
        
        return this;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getHashCode</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the approximate hash value of the role eligibility
    /// </summary>
    /// <returns>The hash value as an integer</returns>
    public int getHashCode()
    {
        return userId.GetHashCode() ^ real2int(roleId);
    }

]]></Source>
			</Method>
			<Method>
				<Name>Equals</Name>
				<Source><![CDATA[
    /// <summary>
    /// Is two records identical
    /// </summary>
    /// <param name = "_obj">The object to compare against</param>
    /// <returns>True if the objects is idential to this</returns>
    public boolean Equals(System.Object _obj)
    {
        FTBXPRMSelfServiceUserEligibleRole contract = _obj;

        return this.userId == contract.userId
            && this.roleId == contract.roleId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>findEligibleRolesForUser</Name>
				<Source><![CDATA[
    /// <summary>
    /// Finds all eligibile roles for an user, at this date/time
    /// </summary>
    /// <param name = "_userId">User that wants to request a role</param>
    /// <returns>Set of all eligible roles for the user</returns>
    public static Set findEligibleRolesForUser(UserId _userId)
    {
        UserGroupList                   userGroupList;
        FTBXPRMSelfServiceEligibleRoles eligibleRoles;        

        Set roleSet = new Set(Types::Class);

        while select * from eligibleRoles
            where eligibleRoles.UserCode        == TableGroupAll::Table
            &&    eligibleRoles.UserValue       == _userId
            &&    eligibleRoles.FromDateTime    <= DateTimeUtil::getSystemDateTime()
            &&   (eligibleRoles.ToDateTime      >= DateTimeUtil::getSystemDateTime() || eligibleRoles.ToDateTime == DateTimeUtil::minValue())
        {
            new FTBXPRMSelfServiceUserEligibleRole(_userId, eligibleRoles.SysSecurityRoleRecId)
                .setUserEligibilityRecord(eligibleRoles)
                .appendToSetIfNotExists(roleSet);
        }

        while select * from eligibleRoles
            where eligibleRoles.UserCode        == TableGroupAll::GroupId
            &&    eligibleRoles.FromDateTime    <= DateTimeUtil::getSystemDateTime()
            &&   (eligibleRoles.ToDateTime      >= DateTimeUtil::getSystemDateTime() || eligibleRoles.ToDateTime == DateTimeUtil::minValue())
            exists join userGroupList
                where userGroupList.groupId == eligibleRoles.UserValue
                &&    userGroupList.userId  == _userId
        {
            new FTBXPRMSelfServiceUserEligibleRole(_userId, eligibleRoles.SysSecurityRoleRecId)
                .setUserEligibilityRecord(eligibleRoles)
                .appendToSetIfNotExists(roleSet);
        }

        while select * from eligibleRoles
            where eligibleRoles.UserCode        == TableGroupAll::All
            &&    eligibleRoles.FromDateTime    <= DateTimeUtil::getSystemDateTime()
            &&   (eligibleRoles.ToDateTime      >= DateTimeUtil::getSystemDateTime() || eligibleRoles.ToDateTime == DateTimeUtil::minValue())
        {
            new FTBXPRMSelfServiceUserEligibleRole(_userId, eligibleRoles.SysSecurityRoleRecId)
                .setUserEligibilityRecord(eligibleRoles)
                .appendToSetIfNotExists(roleSet);
        }

        return roleSet;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>